plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.dokka)
    alias(libs.plugins.detekt)
}

android {
    namespace 'com.verygoodsecurity.api'
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

detekt {
    buildUponDefaultConfig = false
    allRules = false
    config = files("$project.rootDir/.detekt/config.yml")
}

dependencies {
    api project(':vgscollect')

    api libs.cardio

    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx

    dokkaHtmlPlugin(libs.dokka.base)
}

//uncomment if need to force user to stop using this
//tasks.register('deprecatedModuleCheck') {
//
//    doLast {
//        throw new org.gradle.api.GradleException("""
//****************************************************************************************
//❌ ERROR: Module '${project.name}' is DEPRECATED and will be removed in a future release.
//Please migrate to 'blinkcard' adapter as soon as possible.
//****************************************************************************************
//        """.stripIndent())
//    }
//}
//
//tasks.all { task ->
//    if (task.name != "clean" && task.name != "deprecatedModuleCheck") {
//        task.dependsOn(tasks.named('deprecatedModuleCheck'))
//    }
//}

gradle.projectsEvaluated {
    if (project.state.executed) {
        logger.warn("****************************************************************************************")
        logger.warn("⚠️ WARNING: Module '${project.name}' is DEPRECATED and will be removed in a future release.")
        logger.warn("         Please migrate to 'blinkcard' adapter as soon as possible.")
        logger.warn("****************************************************************************************")
    }
}