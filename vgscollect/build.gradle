plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.dokka)
    alias(libs.plugins.detekt)
    alias(libs.plugins.kover)
}

android {
    namespace 'com.verygoodsecurity.vgscollect'
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "VERSION_NAME", "\"$VERSION_NAME\""
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jvmArgs '-noverify'
            }
        }
    }
}

detekt {
    buildUponDefaultConfig = false
    allRules = false
    config = files("$project.rootDir/.detekt/config.yml")
}

dependencies {
    implementation project(':vgs-sdk-analytics:analytics')

    api libs.material
    api libs.okhttp

    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx

    implementation libs.compose.material

    testImplementation testLibs.junit
    testImplementation testLibs.mockito.core
    testImplementation testLibs.mockito.inline
    testImplementation testLibs.robolectric
    testImplementation testLibs.json
    testImplementation testLibs.jsonassert

    androidTestImplementation androidTestLibs.androidx.runner
    androidTestImplementation androidTestLibs.androidx.junit.ext
    androidTestImplementation androidTestLibs.androidx.espresso.core

    dokkaHtmlPlugin(libs.dokka.base)
}

tasks.withType(Javadoc).configureEach {
    enabled = false
}

tasks.named("dokkaHtml") {
    outputDirectory.set(rootProject.file('docs'))
}