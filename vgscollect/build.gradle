plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id 'com.vanniktech.maven.publish'
    id 'io.gitlab.arturbosch.detekt'
    id 'org.jetbrains.kotlinx.kover'
}

android {
    namespace 'com.verygoodsecurity.vgscollect'
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "VERSION_NAME", "\"$VERSION_NAME\""
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jvmArgs '-noverify'
            }
        }
    }
}

detekt {
    buildUponDefaultConfig = false
    allRules = false
    config = files("$project.rootDir/.detekt/config.yml")
}

dependencies {
    api libs.material
    api libs.okhttp

    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx

    implementation platform('androidx.compose:compose-bom:2024.02.00')
    implementation libs.compose.material

    testImplementation testLibs.junit
    testImplementation testLibs.mockito.core
    testImplementation testLibs.mockito.inline
    testImplementation testLibs.robolectric
    testImplementation testLibs.mockk
    testImplementation testLibs.json
    testImplementation testLibs.jsonassert

    androidTestImplementation androidTestLibs.androidx.runner
    androidTestImplementation androidTestLibs.androidx.junit.ext
    androidTestImplementation androidTestLibs.androidx.espresso.core

    dokkaHtmlPlugin(libs.dokka.base)
}

tasks.withType(Javadoc).all {
    enabled = false
}

tasks.named("dokkaHtml") {
    outputDirectory.set(rootProject.file('docs'))
}